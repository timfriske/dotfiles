#!/usr/bin/env -S awk -f

# Kenjo Export Mobile Work command
# ================================
# Tim Friske <me@tifr.de>
#
# Transforms the text that can be copied and pasted from the [Time off
# history](https://app.kenjo.io/cloud/time-off/personal) into a CSV.
#
# Two-digit years equal or above 70 are interpreted as years in the 19th
# century, everything below as years in the 20th century. The CSV can be
# imported into LibreOffice Calc, the number of net work days calculated with
# the `NETWORKDAYS` function and the total net work days with the `SUMIF`
# function depending on the state (ACCEPTED etc).
#
# The CSV format looks like:
# [source,text]
# ----
# 2022-12-19;2022-12-23;Mobile working;ACCEPTED
# 2022-08-04;2022-08-05;Mobile working;CANCELLED
# 2022-04-29;2022-04-29;Mobile working;SUBMITTED
# ----

BEGIN {
  RS="keyboard_arrow_down"
  FS="\n"
  OFS=";"
}

FNR>1 {
  split($2,a,/\s-\s/)
  split(a[1],b,".")
  split(a[2],c,".")
  cent_begin=b[3]>=70?19:20
  cent_end=c[3]>=70?19:20
  print cent_begin""b[3]"-"b[2]"-"b[1],cent_end""c[3]"-"c[2]"-"c[1],$3,$4
}
